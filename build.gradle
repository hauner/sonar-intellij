apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'


version = '1.2-SNAPSHOT'


repositories {
    // IDEAs libs are only available from a IDEA installation
    // 'intellij_home' should be defined in gradle.properties
    flatDir name: 'IntelliJ SDK',  dirs: [
        "${intellij_home}/lib",
        "${intellij_home}/plugins/maven/lib",
        "${intellij_home}/plugins/gradle/lib"
    ]

    // spock
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots/'
    }

    mavenCentral()
    jcenter()
}


configurations {
    provided
}

dependencies {
    // IntelliJ
    provided ':annotations'
    provided ':extensions'
    provided ':openapi'
    provided ':idea'
    provided ':util'
    provided ':maven'
    provided ':maven-server-api'
    provided ':gradle'
    provided ':gradle-tooling-extension-api'
    provided ':forms_rt'
    provided ':sanselan:0.98-snapshot'
    provided ':jsr305:1.3.9'

    testRuntime ':picocontainer'
    testRuntime ':trove4j'
    testRuntime ':jdom'

    // sonar
    compile 'org.codehaus.sonar:sonar-ws-client:4.2'
    compile 'org.codehaus.sonar.runner:sonar-runner-api:2.3'
    compile ('org.apache.httpcomponents:httpclient:4.2.2') {
        exclude module: 'commons-logging'
    }

    // gradle
    provided gradleApi()

    // test
    testCompile 'org.mockito:mockito-core:1.10.0'
    testCompile 'org.easytesting:fest-assert:1.4'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.3-SNAPSHOT'
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }

    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}


task copyResourceToSandbox(type: Copy) {
    from sourceSets.main.output.resourcesDir
    into "${intellij_sandbox}/classes"
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}
